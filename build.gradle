buildscript {
    ext.kotlin_version = '1.0.0-beta-+'

    repositories {
        jcenter()

        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.3"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.2'
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

repositories {
    jcenter()

    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compile 'org.apache.commons:commons-math3:3.5'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    compile files("$rootDir/lib/yeppp-bundle-1.0.jar")
    compile files("$rootDir/lib/simd.jar",
                  "$rootDir/lib/simd-sources.jar")

    testCompile 'junit:junit:4.+'
}

dokka {
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/JetBrains-Research/viktor/blob/master/src/main/kotlin"
        suffix = "#L"
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    group = groupId
    version = project.version

    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'viktor'
        licenses = ['MIT']
        version {
            name = project.version.toString()
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
